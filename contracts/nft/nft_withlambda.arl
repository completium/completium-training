archetype nft(admin : address)

asset ledger {
  id         : nat;
  owner      : address;
}

record auction {
  bestbid    : tez = 0tz;
  bestbidder : option<address> = none;
  endofbid   : date = now;
}

variable auctions : map<nat, auction> = []

variable exec_bid : option<lambda<auction, (list<operation> * option<auction>)>> = lambda(a ->
  if now >= a.endofbid or transferred > a.bestbid then
    fail("InvalidCondition");
  match a.bestbidder with
  | some b -> begin
    var e   : contract<unit> = b;
    var op  : operation = mkoperation(a.bestbid, e, ());
    end
  | none -> ()
  end;
  var %end = a.endofbid;
  var newa = {
    bestbidder = some(caller);
    bestbid = transferred;
    endofbid = (if %end- now < 2m then %end + 2m else %end)
  };
  return (([], some(newa)));
)

entry %transfer (%from : address, %to : address, tid : nat) {
  called by ledger[tid].owner
  effect {
    ledger[tid].owner := %to;
  }
}

entry mint(o : address, p : tez) {
  called by admin
  effect {
    ledger.add({ id = ledger.count(); owner = o })
  }
}

entry upforsale (tid : nat, initprice : tez) {
  called by ledger[tid].owner
  effect {
    auctions := put(auctions, tid,{
      bestbid  = initprice;
      endofbid = (now + 5m)
    });
  }
}

entry bid(tid : nat) {
  require {
    r1 otherwise "Asset Not For Sale": contains(auctions, tid);
  }
  effect {
    match exec_bid with
    | some l -> begin
      var res = exec_lambda(l, auctions[tid]);
      var ops = res[0];
      var val = res[1];
      auctions := update(auctions, tid, val);
      operations := ops;
      end
    | none -> ()
    end
  }
}

entry set_exec_bid (l : option<lambda<auction, (list<operation> * option<auction>)>>) {
  called by admin
  effect { exec_bid := l }
}

entry claim (tid : nat) {
  require {
    r4 otherwise "Bid Period Is Still On": now > auctions[tid].endofbid;
    r5: contains(auctions, tid);
  }
  effect {
    match auctions[tid].bestbidder with
    | some b ->
      transfer auctions[tid].bestbid to ledger[tid].owner;
      auctions := update(auctions, tid, none<auction>)
    | none -> ()
    end
  }
}
