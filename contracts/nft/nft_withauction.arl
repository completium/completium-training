archetype nft(admin : address)

asset ledger {
  id         : nat;
  owner      : address;
}

asset auction {
  aid        : nat;
  bestbid    : tez = 0tz;
  bestbidder : option<address> = none;
  endofbid   : date = now;
}

entry %transfer (%from : address, %to : address, tid : nat) {
  called by ledger[tid].owner
  effect {
    ledger[tid].owner := %to;
  }
}

entry mint(o : address, p : tez) {
  called by admin
  effect {
    ledger.add({ id = ledger.count(); owner = o })
  }
}

entry upforsale (tid : nat, initprice : tez) {
  called by ledger[tid].owner
  effect {
    auction.add({
      aid      = tid;
      bestbid  = initprice;
      endofbid = (now + 5m)
    });
  }
}

entry bid(tid : nat) {
  require {
    r1 otherwise "Asset Not For Sale": auction.contains(tid);
    r2: now < auction[tid].endofbid;
    r3: transferred > auction[tid].bestbid;
  }
  effect {
    match auction[tid].bestbidder with
    | some b ->
      transfer auction[tid].bestbid to b
    | none -> ()
    end;
    auction.update(tid, {
      bestbidder = some(caller);
      bestbid = transferred;
      endofbid += (if auction[tid].endofbid - now < 2m then 2m else 0)
    })
  }
}

entry claim (tid : nat) {
  require {
    r4 otherwise "Bid Period Is Still On": now > auction[tid].endofbid;
    r5: auction.contains(tid);
  }
  effect {
    match auction[tid].bestbidder with
    | some b ->
      transfer auction[tid].bestbid to ledger[tid].owner;
      auction.remove(tid)
    | none -> ()
    end
  }
}
